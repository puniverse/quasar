apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

ext.quasarJar = "${libsDir}/quasar-core-${version}-jdk8.jar" // project(':quasar-core').jar.archivePath

dependencies {
    compile(project(':quasar-core')) { // 
        exclude module: "jsr166e"
    }
}

sourceSets {
    test {
        java {
            srcDirs += "../quasar-core/src/test/java" // project(":quasar-core").sourceSets.test.java.srcDirs
        }
        resources {
            srcDirs += "../quasar-core/src/test/resources" // project(":quasar-core").sourceSets.test.resources.srcDirs
            runtimeClasspath += files("../quasar-core/build/resources/test") // project(':quasar-core').sourceSets.test.resources
        }
    }
}

compileJava {
    doLast {
        ant.taskdef(
            name:'scanSuspendables', classname:'co.paralleluniverse.fibers.instrument.SuspendablesScanner', 
            classpath: "build/classes/main:build/resources/main:" + configurations.compile.asPath)
        ant.scanSuspendables(
            auto: false,
            supersFile:"$sourceSets.main.output.resourcesDir/META-INF/suspendable-supers",
            append: false) {
            fileset(dir: project(':quasar-core').sourceSets.main.output.classesDir)
            fileset(dir: sourceSets.main.output.classesDir)
        }
        
        ant.taskdef(name:'instrumentation', classname:'co.paralleluniverse.fibers.instrument.InstrumentationTask', classpath: "build/resources/main:" + "build/classes/main:" + configurations.compile.asPath)
        ant.instrumentation(verbose:'true', check:'true', debug:'true') {
            fileset(dir: sourceSets.main.output.classesDir) {
                exclude(name: 'co/paralleluniverse/fibers/instrument/*.class')
            }
        }
                
    }
}
    
//task jar(dependsOn: classes, type: Jar, overwrite: true) {
jar.dependsOn ':quasar-core:jar'
jar {
    baseName = 'quasar-core'
    classifier = 'jdk8'
   
    from(project(':quasar-core').sourceSets.main.output.classesDir) {
        exclude fileTree(project.sourceSets.main.output.classesDir).collect { it.path - "${project.sourceSets.main.output.classesDir.path}/" }
    }
    from(project(':quasar-core').sourceSets.main.resources.srcDirs) {
        exclude fileTree(project.sourceSets.main.resources.srcDirs.iterator().next()).collect { it.path - "${project.sourceSets.main.resources.srcDirs.path}/" }
    }
//    from project.sourceSets.main.output.classesDir
//    from project.sourceSets.main.resources

    from zipTree("${baselib}/high-scale-lib.jar")  // include non-Maven jars

    manifest { 
        attributes(
	            "Built-By"                  :   System.getProperty("user.name"),
	            "Implementation-Title"      :   project.name,
	            "Implementation-Version"    :   version,
	            "Implementation-Vendor"     :   vendor,
	            "Premain-Class"             :   "co.paralleluniverse.fibers.instrument.JavaAgent",
	            "Can-Retransform-Classes"   :   "true",
                    "Can-Redefine-Classes"      :   "true",
            //  "Class-Path"                :   configurations.runtime.collect { "lib/"+it.getName() }.join(',') 
        )
    }
}

shadowJar {
    outputs.upToDateWhen { false }
        
    // artifactAttached = false
    baseName = 'quasar-core'
    classifier = 'jdk8-shadow'
    destinationDir = file("$buildDir/libs")

    from(project(':quasar-core').sourceSets.main.output.classesDir) {
        exclude fileTree(project.sourceSets.main.output.classesDir).collect { it.path - "${project.sourceSets.main.output.classesDir.path}/" }
    }
    from(project(':quasar-core').sourceSets.main.resources.srcDirs) {
        exclude fileTree(project.sourceSets.main.resources.srcDirs.iterator().next()).collect { it.path - "${project.sourceSets.main.resources.srcDirs.path}/" }
    }
//    from project.sourceSets.main.output.classesDir
//    from project.sourceSets.main.resources
    
    dependencies {
        include(dependency('org.ow2.asm:.*:.*'))
        include(dependency(':high-scale-lib'))
    }

    relocate 'org.objectweb.asm.', 'co.paralleluniverse.asm.'
        
    doLast {
        logger.info("$buildDir/libs/${baseName}-${project.version}-${classifier}.jar -> $buildDir/libs/${baseName}-${project.version}-${jar.classifier}.jar")
        file("$buildDir/libs/${baseName}-${project.version}-${classifier}.jar").renameTo(file("$buildDir/libs/${baseName}-${project.version}-${jar.classifier}.jar"))
    }
}
    
shadowJar.dependsOn jar
build.dependsOn shadowJar
install.dependsOn shadowJar
signArchives.dependsOn shadowJar
uploadArchives.dependsOn shadowJar
    
uploadArchives.enabled = false
install.enabled = false

tasks.withType(Test) { 
    allJvmArgs = []
    useJUnit()
    // systemProperty 'co.paralleluniverse.fibers.verifyInstrumentation', 'true'
        
    // systemProperty "java.util.logging.config.file", "jul.properties"
    systemProperty "log4j.configurationFile", "${rootProject.projectDir}/log4j.xml"
    // systemProperty "Log4jContextSelector", "org.apache.logging.log4j.core.async.AsyncLoggerContextSelector"
	    
    // systemProperty 'co.paralleluniverse.debugMode', 'true'
    systemProperty 'co.paralleluniverse.globalFlightRecorder', 'true'
    systemProperty 'co.paralleluniverse.flightRecorderDumpFile', 'quasar.log'
    systemProperty 'co.paralleluniverse.monitoring.flightRecorderSize', '200000'
        
    jvmArgs "-javaagent:${libsDir}/quasar-core-${version}-jdk8.jar" // =vdmc (verbose, debug, allow monitors, check class)
    jvmArgs "-Xmx2048m"
    doLast() {
        println "file://" + getReports().getHtml().getDestination() + "/index.html"
    }  
}
