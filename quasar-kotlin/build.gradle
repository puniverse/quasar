plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'org.javamodularity.moduleplugin'
}

ext {
    moduleName = 'co.paralleluniverse.quasar.kotlin'
}

sourceCompatibility = '11'
targetCompatibility = '11'

configurations.all {
    resolutionStrategy {
        dependencySubstitution {
            substitute module('org.jetbrains.kotlin:kotlin-stdlib') with module("org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion")
            substitute module('org.jetbrains.kotlin:kotlin-stdlib-common') with module("org.jetbrains.kotlin:kotlin-stdlib-common:$kotlinVersion")
        }
    }
}

dependencies {
    implementation project(path: ':quasar-core', configuration: 'shadowedJar')
    implementation project(':quasar-actors')

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
}

sourceSets {
    AnnotationTest {
        compileClasspath += main.compileClasspath + main.output + test.compileClasspath + test.output
        runtimeClasspath += main.runtimeClasspath + test.runtimeClasspath
    }
}

configurations {
    AnnotationTestImplementation.extendsFrom implementation
    AnnotationTestRuntimeOnly.extendsFrom runtimeOnly
}

task AnnotationTest(type: Test) {
    description = 'Custom Annotation Tests'
    group = 'verification'

    testClassesDirs = sourceSets.AnnotationTest.output.classesDirs
    classpath = sourceSets.AnnotationTest.runtimeClasspath

    jvmArgs = jvmArgs.findAll {!it.contains("javaagent")}
    jvmArgs "-javaagent:${quasarJar}=a(SUSPENDABLE=Lco/paralleluniverse/kotlin/fibers/MySuspendable;,Lco/paralleluniverse/kotlin/fibers/DennisSuspendable;)"
}

test.dependsOn AnnotationTest

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "11"
    }
}

classes {
    doLast {
        scanAndInstrument(sourceSets.main, [configurations.provided, configurations.runtimeClasspath])
    }
}
